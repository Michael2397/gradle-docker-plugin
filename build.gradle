import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.0'
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    def dependencyVersions = [
            'com.squareup.okio:okio:1.13.0',
            'org.codehaus.groovy:groovy-json:2.4.10',
            'org.codehaus.groovy:groovy-all:2.4.10',
            'org.slf4j:slf4j-api:1.7.25',
            'xerces:xercesImpl:2.6.2',
            'xerces:xmlParserAPIs:2.6.2',
            'xml-apis:xml-apis:1.0.b2'
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
    distributionType = Wrapper.DistributionType.ALL
}
